plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.1'
    id 'maven-publish'
}

group = 'ru.ckateptb'
version = '1.0-SNAPSHOT'

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'codemc-snapshots'
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }
    maven {
        name = 'minebench-repo'
        url = 'https://repo.minebench.de/'
    }
    maven {
        name = 'dmulloy2-repo'
        url = 'https://repo.dmulloy2.net/repository/public/'
    }
    maven {
        name = 'jitpack.io'
        url = 'https://jitpack.io'
    }
    maven { url = "https://repo.codemc.org/repository/maven-public/" }
}

shadowJar {
    archiveClassifier.set('')
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
    relocate 'net.wesjd.anvilgui', 'ru.ckateptb.tablecloth.gui.anvil'
    relocate 'xyz.upperlevel.spigot.book', 'ru.ckateptb.tablecloth.gui.book'
    relocate 'de.themoep.minedown', 'ru.ckateptb.tablecloth.minedown'
    relocate 'com.j256.ormlite', 'ru.ckateptb.tablecloth.storage.ormlite'
    relocate 'com.zaxxer.hikari', 'ru.ckateptb.tablecloth.storage.hikari'
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    compileOnly 'org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT'

    implementation 'org.springframework:spring-context:5.3.9'
    implementation 'net.wesjd:anvilgui:1.5.3-SNAPSHOT'
    implementation 'xyz.upperlevel.spigot.book:spigot-book-api:1.6'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'de.themoep:minedown:1.7.1-SNAPSHOT'

    implementation 'com.zaxxer:HikariCP:3.4.2'
    implementation 'com.j256.ormlite:ormlite-jdbc:6.0'

    compileOnly 'com.comphenix.protocol:ProtocolLib:4.8.0-SNAPSHOT'
    compileOnly 'dev.jorel.CommandAPI:commandapi-core:6.4.0'
}

build.dependsOn shadowJar

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        library(MavenPublication) {
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
            from components.java
        }
    }
}
